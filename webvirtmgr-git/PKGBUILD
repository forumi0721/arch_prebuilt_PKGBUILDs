# Maintainer: Zachary Hill <zer0t3ch97@gmail.com>

pkgname='webvirtmgr-git'
pkgdesc='Web front-end for KVM virtual machines'
pkgver=v4.8.9.r2.gb73cdaf
pkgrel=1
arch=('i686' 'x86_64')
url='https://www.webvirtmgr.net/'
license=('Apache')
depends=('libvirt' 'libvirt-python' 'qemu' 'dnsmasq' 'bridge-utils' 'ebtables' 'dmidecode' 'python2' 'supervisor'
         'websockify')
makedepends=('git' 'python2-pip')
options=('!strip' '!emptydirs')
install="${pkgname}.install"
source=('git+https://github.com/retspen/webvirtmgr.git'
        'webvirtmgr.ini'
        'webvirtmgr-novnc.service')
sha512sums=('SKIP'
            '09469b807ab79177220f62316c2cbb7da72b4577f17c5a104d106a2392090753422740dc9dfb4f952e2d40e74ca7209ece911f7993dae0720659239d25c28d01'
            '78ae3aef65e4c362a7d70ff3a44d1c3ccd489ed7b77311d0ecb4d795aa587dc75e43356f56e4afbb180e0558859b64cbe78ca45bb234920ff3302b01e59716db')

pkgver() {
  cd 'webvirtmgr'
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  outputdir="${pkgdir}/usr/lib/webvirtmgr"
  install -Dm0644 'webvirtmgr.ini' "${pkgdir}/etc/supervisor.d/webvirtmgr.ini"
  install -Dm0644 'webvirtmgr-novnc.service' "${pkgdir}/usr/lib/systemd/system/webvirtmgr-novnc.service"
  install -dm0755 "${outputdir}" "${pkgdir}/var/lib/webvirtmgr"
  cp -r "${srcdir}/webvirtmgr" "${pkgdir}/usr/lib"
  rm -rf "${outputdir}/.git"

  cd "${outputdir}"
  pip2 install -r 'requirements.txt' -t "${outputdir}/lib"
}
