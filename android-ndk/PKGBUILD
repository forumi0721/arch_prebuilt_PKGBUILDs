# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=android-ndk
pkgver=r10d
pkgrel=2
pkgdesc='Android C/C++ developer kit'
arch=('x86_64' 'i686')
url='http://developer.android.com/sdk/ndk/'
license=('GPL' 'LGPL' 'custom')
options=('!strip' 'staticlibs')
backup=('etc/profile.d/android-ndk.sh')
install="$pkgname.install"

if [[ $CARCH == i686 ]]; then
  source=("${pkgname}_i686.bin::http://dl.google.com/android/ndk/${pkgname}-$pkgver-linux-x86.bin" 'android-ndk.sh')
md5sums=('70ed6d8c34e7e620c145b791e8eeef89'
         '372f74400bfa061642c4b4175f2ddb48')
else
 source=("${pkgname}_x86_64.bin::http://dl.google.com/android/ndk/${pkgname}-$pkgver-linux-x86_64.bin" 'android-ndk.sh')
md5sums=('263b83071e6bca15f67898548d8d236e'
         '372f74400bfa061642c4b4175f2ddb48')
fi

prepare() {
  chmod +x ${pkgname}_$CARCH.bin
  ./${pkgname}_$CARCH.bin
}

package() {
  provides=('android-ndk')
  replaces=('android-ndk32' 'android-ndk64')

  install -d "$pkgdir/opt"
  mv "$pkgname-$pkgver" "$pkgdir/opt/$pkgname"

  install -Dm755 android-ndk.sh "$pkgdir/etc/profile.d/android-ndk.sh"

  # Fix broken permissions
  chmod -R o=g "$pkgdir/opt/$pkgname"
  find "$pkgdir/opt/$pkgname" -perm 744 -exec chmod 755 {} +
}

# vim:set ts=2 sw=2 et:
